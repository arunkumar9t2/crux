buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven'

group = 'com.chimbori.crux'
version = '2.0.1'

description = """Crux parses Web pages to identify the crux of an article — the very essential 
  points — minus all the fluff. The library consists of multiple independent APIs. You can pick and 
  choose which ones you want to use. If you use Crux in an Android app, they are designed to be  
  independent so that Proguard or other minification tools can strip out the parts you don’t use."""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
}

dependencies {
  // https://github.com/jhy/jsoup/issues/911 prevents upgrading to JSoup 1.11.2.
  compile 'org.jsoup:jsoup:1.9.2'
  compile 'org.apache.commons:commons-lang3:3.7'
  testCompile 'junit:junit:4.12'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task writeNewPom {
  doLast {
    pom {
      project {
        inceptionYear '2016'
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
      }
    }.writeTo("$buildDir/newpom.xml")
  }
}
